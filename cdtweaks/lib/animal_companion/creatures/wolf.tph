/*
+-------------+
| SPL file(s) |
+-------------+
*/

LAF "GT_ADD_SPELL"
INT_VAR
	"type" = 3
	"level" = 5
STR_VAR
	"idsName" = "INNATE_WINTER_WOLF_FROST_BREATH"
RET
	"INNATE_WINTER_WOLF_FROST_BREATH" = "resName"
END

WITH_SCOPE BEGIN
	ACTION_IF !(FILE_EXISTS_IN_GAME "%INNATE_WINTER_WOLF_FROST_BREATH%.spl") BEGIN
		ACTION_TO_LOWER "INNATE_WINTER_WOLF_FROST_BREATH"
		//
		CREATE "spl" "%INNATE_WINTER_WOLF_FROST_BREATH%"
		COPY_EXISTING "%INNATE_WINTER_WOLF_FROST_BREATH%.spl" "override"
			/* Header */
			WRITE_LONG NAME1 RESOLVE_STR_REF (@8000)
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC RESOLVE_STR_REF (@8001)
			WRITE_LONG DESC "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // Ignore dead/wild magic, Castable when silenced
			WRITE_SHORT 0x1C 4 // Type: Innate
			WRITE_LONG 0x34 1 // Level
			WRITE_ASCII 0x3A ~%WIZARD_CONE_OF_COLD%C~ #8 // Icon
			/* Extended Header */
			PATCH_WITH_SCOPE BEGIN
				FOR ("i" = 5 ; "%i%" <= 40 ; "i" += 5) BEGIN
					LPF ~ADD_SPELL_HEADER~ INT_VAR "required_level" = ("%i%" == 5 ? 1 : "%i%") "range" = 25 "projectile" = IDS_OF_SYMBOL ("MISSILE" "New_Cone_Of_Cold") "target" = 4 STR_VAR "icon" = ~%WIZARD_CONE_OF_COLD%B~ END
					/* Feature Blocks */
					LPF "ADD_SPELL_EFFECT" INT_VAR "header" = SHORT_AT 0x68 "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "COLD") "dicenumber" = 6 "dicesize" = 4 "savingthrow" = BIT1 "savebonus" = "-1" * (("%i%" / 5) - 1) "special" = BIT8 END // Damage (6d4, save vs. breath for half)
				END
			END
		BUT_ONLY
	END
END

/*
+----------+
| CRE file |
+----------+
*/

WITH_SCOPE BEGIN
	ACTION_TO_LOWER "BEASTMASTER_ANIMAL_COMPANION"
	//
	CREATE "cre" "%BEASTMASTER_ANIMAL_COMPANION%h"
	COPY_EXISTING "%BEASTMASTER_ANIMAL_COMPANION%h.cre" "override"
		LPF "WRITE_CRE_HEADER"
		INT_VAR
			"name" = RESOLVE_STR_REF (@8002)
			"tooltip" = RESOLVE_STR_REF (@8002)
			//
			"flags" = BIT1 // no corpse
			//
			"curHP" = 1
			"maxHP" = 1
			//
			"animation" = IDS_OF_SYMBOL ("animate" "WOLF_WINTER")
			//
			"metal_color" = 20
			"minor_color" = 31
			"major_color" = 31
			"skin_color" = 7
			"leather_color" = 14
			"armor_color" = 20
			"hair_color" = 0
			//
			"cold_resistance" = 125
			"fire_resistance" = "-25"
			//
			"AC" = 5
			//
			"apr" = 1 // # attacks/round
			//
			"sex" = IDS_OF_SYMBOL ("gender" "MALE")
			// abilities
			"STR" = 18
			"INT" = 10
			"WIS" = 8
			"DEX" = 12
			"CON" = 16
			"CHR" = 6
			//
			"morale" = 13
			"morale_break" = 6
			"morale_recovery" = 30
			// IDS
			"allegiance" = IDS_OF_SYMBOL ("ea" "NEUTRAL")
			"general" = IDS_OF_SYMBOL ("general" "ANIMAL")
			"race" = IDS_OF_SYMBOL ("race" "WOLF")
			"class" = IDS_OF_SYMBOL ("class" "WOLF_WINTER")
			"gender" = IDS_OF_SYMBOL ("gender" "MALE")
			"alignment" = IDS_OF_SYMBOL ("align" "NEUTRAL_EVIL")
		STR_VAR
			"script_override" = "%BEASTMASTER_ANIMAL_COMPANION%x"
			"script_class" = "%BEASTMASTER_ANIMAL_COMPANION%y"
			"script_default" = "%BEASTMASTER_ANIMAL_COMPANION%z"
			//
			"deathvar" = "gtAnmlCompWolf"
		END
		// equipment
		ADD_CRE_ITEM "gtpet08a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
		// passive traits
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "%BEASTMASTER_ANIMAL_COMPANION%#" END // cast spell onto myself upon death (suppress spell's name)
		// spellbook
		ADD_MEMORIZED_SPELL "%INNATE_WINTER_WOLF_FROST_BREATH%" #0 "innate"
		ADD_MEMORIZED_SPELL "%BEASTMASTER_ANIMAL_COMPANION%#" #0 "innate" // banish
	BUT_ONLY_IF_IT_CHANGES
END

/*
+-------------+
| ITM file(s) |
+-------------+
*/

WITH_SCOPE BEGIN
	CREATE "itm" "gtpet08a"
	COPY_EXISTING "gtpet08a.itm" "override"
		/* Header */
		WRITE_LONG NAME1 "-1"
		WRITE_LONG NAME2 "-1"
		WRITE_LONG UNIDENTIFIED_DESC "-1"
		WRITE_LONG DESC "-1"
		WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
		WRITE_SHORT 0x38 1 // maximum in stack
		WRITE_ASCII 0x3A "IWOLF" #8
		/* Extended Header */
		LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 2 "dicesize" = 4 "damage_type" = 1 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 STR_VAR "icon" = "IWOLF" END // 2d4 (piercing)
	BUT_ONLY_IF_IT_CHANGES
	//
	WITH_SCOPE BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY "gtWinterWolfWeaponUpgrades" BEGIN
			"b" => 0 // magical (+0)
			"c" => 1 // magical (+1)
			"d" => 2 // magical (+2)
			"e" => 3 // magical (+3)
		END
		ACTION_PHP_EACH "gtWinterWolfWeaponUpgrades" AS "id" => "enchantment" BEGIN
			COPY_EXISTING "gtpet08a.itm" "override\gtpet08%id%.itm"
				WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
				WRITE_LONG 0x60 "%enchantment%"
				//
				LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%enchantment%" "thac0_bonus" = "%enchantment%" END
			BUT_ONLY_IF_IT_CHANGES
		END
	END
END

/*
+-------------+
**LUA file(s) |
+-------------+
*/