/*
+----------+
| CRE file |
+----------+
*/

// creature animation files
WITH_SCOPE BEGIN
	ACTION_IF IDS_OF_SYMBOL ("animate" "GT_NWN_FALCON") == "-1" BEGIN
		COPY "cdtweaks\luke\animation\falcon" "override"
		LAF "FIND_FREE_ANIM_SLOT" INT_VAR "slotMin" = 0xE000 RET "slot" END
		OUTER_PATCH "" BEGIN
			SPRINTF "hexNumber" "%x" ("%slot%")
		END
		OUTER_PATCH_SAVE "hexNumber" "%hexNumber%" BEGIN
			REPLACE_TEXTUALLY CASE_SENSITIVE EVALUATE_REGEXP "^0x" ""
		END
		COPY "cdtweaks\luke\ini\e###.ini" "override\%hexNumber%.ini"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "PLACEHOLDER" "GT_NWN_FALCON"
			// [monster_icewind]
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "resref=" "resref=gtfc"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "detected_by_infravision=0" "detected_by_infravision=1"
			// [sounds]
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "attack=" "attack=amb_e43a,blank,blank"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "die=" "die=amb_e43b"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "head_turn=blank,blank,blank,blank,blank,blank" "head_turn=amb_e43a,amb_e43b,blank,blank,blank,blank"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "attack_slash=" "attack_slash=amb_e43a,blank,blank"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "attack_backslash=" "attack_backslash=amb_e43a,blank,blank"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "attack_jab=" "attack_jab=amb_e43a,blank,blank"
			REPLACE_TEXTUALLY CASE_SENSITIVE EXACT_MATCH "battle_cry=" "battle_cry=amb_e43a"
		BUT_ONLY
		APPEND "animate.ids" "0x%hexNumber% GT_NWN_FALCON"
	END
END

WITH_SCOPE BEGIN
	ACTION_TO_LOWER "BEASTMASTER_ANIMAL_COMPANION"
	//
	CREATE "cre" "%BEASTMASTER_ANIMAL_COMPANION%d"
	COPY_EXISTING "%BEASTMASTER_ANIMAL_COMPANION%d.cre" "override"
		LPF "WRITE_CRE_HEADER"
		INT_VAR
			"name" = RESOLVE_STR_REF (@4000)
			"tooltip" = RESOLVE_STR_REF (@4000)
			//
			"flags" = BIT1 // no corpse
			//
			"curHP" = 1
			"maxHP" = 1
			//
			"animation" = IDS_OF_SYMBOL ("animate" "GT_NWN_FALCON")
			//
			"metal_color" = 30
			"minor_color" = 37
			"major_color" = 1
			"skin_color" = 9
			"leather_color" = 23
			"armor_color" = 30
			"hair_color" = 1
			//
			"AC" = 5
			//
			"apr" = 3 // # attacks/round
			//
			"sex" = IDS_OF_SYMBOL ("gender" "MALE")
			// abilities
			"STR" = 5
			"INT" = 1
			"WIS" = 14
			"DEX" = 16
			"CON" = 8
			"CHR" = 6
			//
			"morale" = 6
			"morale_break" = 5
			"morale_recovery" = 60
			// IDS
			"allegiance" = IDS_OF_SYMBOL ("ea" "NEUTRAL")
			"general" = IDS_OF_SYMBOL ("general" "ANIMAL")
			"race" = IDS_OF_SYMBOL ("race" "BIRD")
			"class" = IDS_OF_SYMBOL ("class" "NO_CLASS")
			"gender" = IDS_OF_SYMBOL ("gender" "MALE")
			"alignment" = IDS_OF_SYMBOL ("align" "NEUTRAL")
		STR_VAR
			"script_override" = "%BEASTMASTER_ANIMAL_COMPANION%x"
			"script_class" = "%BEASTMASTER_ANIMAL_COMPANION%y"
			"script_default" = "%BEASTMASTER_ANIMAL_COMPANION%z"
			//
			"deathvar" = "gtAnmlCompFalcon"
		END
		// equipment
		ADD_CRE_ITEM "gtpet04a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
		// passive traits
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 262 "target" = 1 "timing" = 1 "parameter1" = 2 END // +2 visual range
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "%BEASTMASTER_ANIMAL_COMPANION%#" END // cast spell onto myself upon death (suppress spell's name)
		// spellbook
		ADD_MEMORIZED_SPELL "%BEASTMASTER_ANIMAL_COMPANION%#" #0 "innate" // banish
	BUT_ONLY_IF_IT_CHANGES
END

/*
+-------------+
| SPL file(s) |
+-------------+
*/

LAF "GT_ADD_SPELL"
INT_VAR
	"type" = 3
	"level" = 2
	"preferredSlot" = 21
STR_VAR
	"idsName" = "INNATE_HAWK_BLIND"
RET
	"INNATE_HAWK_BLIND" = "resName"
END

WITH_SCOPE BEGIN
	ACTION_IF !(FILE_EXISTS_IN_GAME "%INNATE_HAWK_BLIND%.spl") BEGIN
		ACTION_TO_LOWER "INNATE_HAWK_BLIND"
		//
		CREATE "spl" "%INNATE_HAWK_BLIND%"
		COPY_EXISTING "%INNATE_HAWK_BLIND%.spl" "override"
			/* Header */
			WRITE_LONG NAME1 "-1"
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG DESC "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // Ignore dead/wild magic, Castable when silenced
			WRITE_SHORT 0x1C 4 // Type: Innate
			WRITE_LONG 0x34 1 // Level
			/* Extended Header */
			LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
			/* Feature Blocks */
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "%DEST_RES%" END // invoke lua
		BUT_ONLY
	END
END

/*
+-------------+
| ITM file(s) |
+-------------+
*/

WITH_SCOPE BEGIN
	CREATE "itm" "gtpet04a"
	COPY_EXISTING "gtpet04a.itm" "override"
		/* Header */
		WRITE_LONG NAME1 "-1"
		WRITE_LONG NAME2 "-1"
		WRITE_LONG UNIDENTIFIED_DESC "-1"
		WRITE_LONG DESC "-1"
		WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
		WRITE_SHORT 0x38 1 // maximum in stack
		WRITE_ASCII 0x3A "IWOLF" #8
		/* Extended Header */
		LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 1 "dicesize" = 1 "damage_type" = 1 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 STR_VAR "icon" = "IWOLF" END // 1d1 (piercing)
		/* Extended Header */
		LPF "ADD_ITEM_EFFECT" INT_VAR "type" = 1 "opcode" = 146 "target" = 2 "parameter2" = 1 "probability1" = 25 "probability2" = 1 STR_VAR "resource" = "%INNATE_HAWK_BLIND%" END // Cast spell
	BUT_ONLY_IF_IT_CHANGES
	//
	WITH_SCOPE BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY "gtFalconWeaponUpgrades" BEGIN
			"b" => 0 // magical (+0)
			"c" => 1 // magical (+1)
			"d" => 2 // magical (+2)
			"e" => 3 // magical (+3)
		END
		ACTION_PHP_EACH "gtFalconWeaponUpgrades" AS "id" => "enchantment" BEGIN
			COPY_EXISTING "gtpet04a.itm" "override\gtpet04%id%.itm"
				WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
				WRITE_LONG 0x60 "%enchantment%"
				//
				LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%enchantment%" "thac0_bonus" = "%enchantment%" END
			BUT_ONLY_IF_IT_CHANGES
		END
	END
END

/*
+-------------+
**LUA file(s) |
+-------------+
*/

WITH_SCOPE BEGIN
	OUTER_SET "feedback_strref_blind" = RESOLVE_STR_REF (@4001)
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Innate Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\kit\animal_companion\falcon\blind.lua" "destRes" = "m_gtspin" END
END
