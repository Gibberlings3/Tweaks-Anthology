/*
+-------------+
| SPL file(s) |
+-------------+
*/

LAF "GT_ADD_SPELL"
INT_VAR
	"type" = 3
	"level" = 1
	"preferredSlot" = 91
STR_VAR
	"idsName" = "INNATE_BOMBARDIER_BEETLE_CLOUD"
RET
	"INNATE_BOMBARDIER_BEETLE_CLOUD" = "resName"
END

WITH_SCOPE BEGIN
	ACTION_IF !(FILE_EXISTS_IN_GAME "%INNATE_BOMBARDIER_BEETLE_CLOUD%.spl") BEGIN
		ACTION_TO_LOWER "INNATE_BOMBARDIER_BEETLE_CLOUD"
		//
		CREATE "spl" "%INNATE_BOMBARDIER_BEETLE_CLOUD%"
		COPY_EXISTING "%INNATE_BOMBARDIER_BEETLE_CLOUD%.spl" "override"
			/* Header */
			WRITE_LONG NAME1 "-1"
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG DESC "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // Ignore dead/wild magic, castable when silenced
			WRITE_SHORT 0x1C 4 // Type: Innate
			WRITE_LONG 0x34 1 // Level
			/* Extended Header */
			LPF ~ADD_SPELL_HEADER~ INT_VAR "target" = 5 "range" = 30 END
			/* Feature Blocks */
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 318 "target" = 1 "parameter1" = IDS_OF_SYMBOL ("state" "STATE_IMMOBILE") BOR IDS_OF_SYMBOL ("state" "STATE_FEEBLEMINDED") "parameter2" = 138 STR_VAR "resource" = "%DEST_RES%" END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 146 "target" = 1 "parameter2" = 1 "probability1" = 50 "probability2" = 1 STR_VAR "resource" = "%DEST_RES%B" END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 206 "target" = 1 "parameter1" = "-1" "duration" = 6 STR_VAR "resource" = "%DEST_RES%" END
		BUT_ONLY
		//
		ADD_PROJECTILE "cdtweaks\luke\pro\gtbeetbm.pro" "GT_Bombardier_Beetle_Cloud"
		WITH_SCOPE BEGIN
			ACTION_FOR_EACH "file" IN "#eff_m47.wav" "sclouda.bam" "scloudr.bam" BEGIN
				ACTION_IF !(FILE_EXISTS_IN_GAME "%file%") BEGIN
					OUTER_SNPRINT "-3" "ext" "%file%"
					COPY "cdtweaks\luke\%ext%\%file%" "override"
				END
			END
		END
		//
		CREATE "spl" "%INNATE_BOMBARDIER_BEETLE_CLOUD%b"
		COPY_EXISTING "%INNATE_BOMBARDIER_BEETLE_CLOUD%b.spl" "override"
			/* Header */
			WRITE_LONG NAME1 "-1"
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG DESC "-1"
			WRITE_LONG 0x18 (BIT14 BOR BIT25) // Ignore dead/wild magic, castable when silenced
			WRITE_SHORT 0x1C 4 // Type: Innate
			WRITE_LONG 0x34 1 // Level
			/* Extended Header */
			LPF ~ADD_SPELL_HEADER~ INT_VAR "target" = 5 "range" = 30 "projectile" = IDS_OF_SYMBOL ("missile" "GT_Bombardier_Beetle_Cloud") END
			/* Feature Blocks */
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "parameter1" = RESOLVE_STR_REF (@2000) END // feedback string
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "%INNATE_BOMBARDIER_BEETLE_CLOUD%" END // Invoke lua
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "ACID") "dicenumber" = 3 "dicesize" = 4 "savingthrow" = BIT24 END // 3d4 (bypass mirror image)
		BUT_ONLY
	END
END

/*
+----------+
| CRE file |
+----------+
*/

// creature animation files
WITH_SCOPE BEGIN
	ACTION_BASH_FOR "cdtweaks\luke\animation\beetle_bombardier" "^.+\.\(bam\|wav\|bmp\|ini\)$" BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "%BASH_FOR_FILE%") BEGIN
			ACTION_TO_LOWER "BASH_FOR_FILESPEC"
			COPY "%BASH_FOR_FILESPEC%" "override"
		END
	END
	APPEND "animate.ids" "0xE220 BEETLE_BLACK" UNLESS "BEETLE_BLACK"
END

WITH_SCOPE BEGIN
	ACTION_TO_LOWER "BEASTMASTER_ANIMAL_COMPANION"
	//
	CREATE "cre" "%BEASTMASTER_ANIMAL_COMPANION%b"
	COPY_EXISTING "%BEASTMASTER_ANIMAL_COMPANION%b.cre" "override"
		LPF "WRITE_CRE_HEADER"
		INT_VAR
			"name" = RESOLVE_STR_REF (@2001)
			"tooltip" = RESOLVE_STR_REF (@2001)
			//
			"flags" = BIT1 // no corpse
			//
			"curHP" = 1
			"maxHP" = 1
			//
			"animation" = IDS_OF_SYMBOL ("animate" "BEETLE_BLACK")
			//
			"metal_color" = 30
			"minor_color" = 28
			"major_color" = 103
			"skin_color" = 28
			"leather_color" = 30
			"armor_color" = 30
			"hair_color" = 28
			//
			"AC" = 4
			//
			"apr" = 1 // # attacks/round
			//
			"sex" = IDS_OF_SYMBOL ("gender" "FEMALE")
			// abilities
			"STR" = 3
			"INT" = 1
			"WIS" = 1
			"DEX" = 10
			"CON" = 14
			"CHR" = 1
			//
			"morale" = 13
			"morale_break" = 3
			"morale_recovery" = 30
			// IDS
			"allegiance" = IDS_OF_SYMBOL ("ea" "NEUTRAL")
			"general" = IDS_OF_SYMBOL ("general" "ANIMAL")
			"race" = IDS_OF_SYMBOL ("race" "BEETLE")
			"class" = IDS_OF_SYMBOL ("class" "NO_CLASS")
			"gender" = IDS_OF_SYMBOL ("gender" "FEMALE")
			"alignment" = IDS_OF_SYMBOL ("align" "NEUTRAL")
		STR_VAR
			"script_override" = "%BEASTMASTER_ANIMAL_COMPANION%x"
			"script_class" = "%BEASTMASTER_ANIMAL_COMPANION%y"
			"script_default" = "%BEASTMASTER_ANIMAL_COMPANION%z"
			//
			"deathvar" = "gtAnmlCompBeetle"
		END
		// equipment
		ADD_CRE_ITEM "gtpet02a" #0 #0 #0 "identified&unstealable&undroppable" "weapon" EQUIP
		// passive traits
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 268 "target" = 1 "timing" = 1 END // clear fog of war
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 16 "special" = BIT2 STR_VAR "resource" = "%BEASTMASTER_ANIMAL_COMPANION%#" END // cast spell onto myself upon death (suppress spell's name)
		LPF "ADD_CRE_EFFECT" INT_VAR "opcode" = 232 "target" = 1 "timing" = 1 "parameter2" = 7 STR_VAR "resource" = "%INNATE_BOMBARDIER_BEETLE_CLOUD%" END // Cast spell on condition (when attacked) -- NB.: ``timing=1`` instead of ``timing=9`` so that the fake contingency does not accidentally trigger if the creature is dead
		// spellbook
		ADD_MEMORIZED_SPELL "%BEASTMASTER_ANIMAL_COMPANION%#" #0 "innate" // banish
	BUT_ONLY_IF_IT_CHANGES
END

/*
+-------------+
| ITM file(s) |
+-------------+
*/

WITH_SCOPE BEGIN
	CREATE "itm" "gtpet02a"
	COPY_EXISTING "gtpet02a.itm" "override"
		/* Header */
		WRITE_LONG NAME1 "-1"
		WRITE_LONG NAME2 "-1"
		WRITE_LONG UNIDENTIFIED_DESC "-1"
		WRITE_LONG DESC "-1"
		WRITE_LONG 0x18 BIT5 // flags: not copyable (not needed...?)
		WRITE_SHORT 0x38 1 // maximum in stack
		//WRITE_ASCII 0x3A "PINCERS" #8
		/* Extended Header */
		LPF ~ADD_ITEM_HEADER~ INT_VAR "type" = 1 "location" = 1 "range" = 1 "dicenumber" = 2 "dicesize" = 6 "damage_type" = 3 "depletion" = 1 "flags" = BIT0 "overhand" = 34 "backhand" = 33 "thrust" = 33 END // 2d6 (Slashing)
	BUT_ONLY_IF_IT_CHANGES
	//
	WITH_SCOPE BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY "gtBeetleWeaponUpgrades" BEGIN
			"b" => 0 // magical (+0)
			"c" => 1 // magical (+1)
			"d" => 2 // magical (+2)
			"e" => 3 // magical (+3)
		END
		ACTION_PHP_EACH "gtBeetleWeaponUpgrades" AS "id" => "enchantment" BEGIN
			COPY_EXISTING "gtpet02a.itm" "override\gtpet02%id%.itm"
				WRITE_LONG 0x18 (THIS BOR BIT6) // flags: magical
				WRITE_LONG 0x60 "%enchantment%"
				//
				LPF "ALTER_ITEM_HEADER" INT_VAR "damage_bonus" = "%enchantment%" "thac0_bonus" = "%enchantment%" END
			BUT_ONLY_IF_IT_CHANGES
		END
	END
END

/*
+-------------+
| LUA file(s) |
+-------------+
*/

WITH_SCOPE BEGIN
	OUTER_SET "feedback_strref_stun" = RESOLVE_STR_REF (@2002)
	OUTER_SET "feedback_strref_deaf" = RESOLVE_STR_REF (@2003)
	//
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Innate Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\kit\animal_companion\beetle\cloud.lua" "destRes" = "m_gtspin" END
END
