DEFINE_ACTION_FUNCTION "CLEAVE"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"level" = 1
		"type" = 4
	STR_VAR
		"idsName" = "FIGHTER_CLEAVE"
	RET
		"FIGHTER_CLEAVE" = "resName"
	END
	//
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "FIGHTER_CLEAVE"
		//
		CREATE "eff" "%FIGHTER_CLEAVE%b"
		COPY_EXISTING "%FIGHTER_CLEAVE%b.eff" "override"
			WRITE_LONG 0x10 402 // invoke lua
			WRITE_SHORT 0x2C 100 // prob1
			WRITE_LONG 0x1C 1 // parameter1
			WRITE_ASCII 0x30 "%FIGHTER_CLEAVE%" #8 // lua function
		BUT_ONLY
		//
		CREATE "spl" "%FIGHTER_CLEAVE%b"
		COPY_EXISTING "%FIGHTER_CLEAVE%b.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@0)
			WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 138 "target" = 1 "timing" = 1 END // Set animation sequence (ATTACK) -- probably not needed...?
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 "parameter1" = 2 STR_VAR "resource" = "%FIGHTER_CLEAVE%" END // invoke lua
		BUT_ONLY_IF_IT_CHANGES
	END
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\class\circle_kick.lua" "destRes" = "m_gtspcl" END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "AI-related stuff" "sourceFileSpec" = "cdtweaks\luke\lib\ai\object_type.lua" "destRes" = "m_gt#ai" END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\damage_type.lua" "destRes" = "m_gtsprt" END
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\check_ids.lua" "destRes" = "m_gtsprt" END
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\get_selected_weapon.lua" "destRes" = "m_gtsprt" END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Eval Conditional" "sourceFileSpec" = "cdtweaks\luke\lib\misc\eval_conditional.lua" "destRes" = "m_gteval" END
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Execute Response" "sourceFileSpec" = "cdtweaks\luke\lib\misc\execute_response.lua" "destRes" = "m_gtexec" END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lib\resource\tables.lua" "override\m_gttbls.lua"
	END
END