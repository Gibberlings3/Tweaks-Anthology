DEFINE_ACTION_FUNCTION "NWN_BARBARIAN_RAGE"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"level" = 1
		"preferredSlot" = 52
	STR_VAR
		"idsName" = "BARBARIAN_RAGE"
	RET
		"BARBARIAN_RAGE" = "resName"
	END
	//
	LAF "ADD_EXTENDED_STAT" STR_VAR "identifier" = "GT_DUMMY_STAT" END
	// update main asset
	WITH_SCOPE BEGIN
		COPY_EXISTING "spcl152.spl" "override"
			WRITE_LONG UNIDENTIFIED_DESC RESOLVE_STR_REF (@0)
			//
			LPF "DELETE_EFFECT" END // fresh start
			// cosmetic
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 215 "target" = 1 "duration" = 2 "parameter2" = 1 STR_VAR "resource" = "ICSTRENI" END // Play visual effect: Over target (attached)
			// lua
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 1 "parameter1" = 1 STR_VAR "resource" = "%BARBARIAN_RAGE%" END // Invoke lua
		BUT_ONLY
	END
	// terrifying rage
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "BARBARIAN_RAGE"
		//
		CREATE "spl" "%BARBARIAN_RAGE%b"
		COPY_EXISTING "%BARBARIAN_RAGE%b.spl" "override"
			WRITE_LONG NAME1 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 (BIT9 BOR BIT10 BOR BIT14) // break sanctuary/invisibility, ignore dead/wild magic
			WRITE_LONG 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			PATCH_WITH_SCOPE BEGIN
				PATCH_IF IDS_OF_SYMBOL ("missile" "Area_Small_Not_Party_Ignore_Center") == "-1" BEGIN
					INNER_ACTION BEGIN
						ADD_PROJECTILE "cdtweaks\luke\pro\idpro402.pro" "Area_Small_Not_Party_Ignore_Center"
					END
				END
			END
			//
			LPF ~ADD_SPELL_HEADER~ INT_VAR "target" = 5 "projectile" = IDS_OF_SYMBOL ("missile" "Area_Small_Not_Party_Ignore_Center") END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 9 "parameter1" = RESOLVE_STR_REF (@1) END // display string
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 141 "target" = 2 "parameter2" = 16 END // lighting effects
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 "parameter1" = 2 STR_VAR "resource" = "%BARBARIAN_RAGE%" END // invoke lua
		BUT_ONLY
	END
	// Thundering Rage
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "BARBARIAN_RAGE"
		// 2d6 upon critical hits
		CREATE "spl" "%BARBARIAN_RAGE%c"
		COPY_EXISTING "%BARBARIAN_RAGE%c.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@2)
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 (BIT9 BOR BIT10 BOR BIT14) // break sanctuary/invisibility, ignore dead/wild magic
			WRITE_LONG 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 "parameter1" = 3 STR_VAR "resource" = "%BARBARIAN_RAGE%" END // Invoke lua
		BUT_ONLY
		// deaf: 25% chance, no save
		CREATE "eff" "%BARBARIAN_RAGE%d"
		COPY_EXISTING "%BARBARIAN_RAGE%d.eff" "override"
			WRITE_LONG 0x10 146 // cast spell
			WRITE_LONG 0x20 1 // instant/ignore level
			WRITE_ASCII 0x30 "%BARBARIAN_RAGE%D" #8 // spl resref
			WRITE_SHORT 0x2C 24 // probability1
		BUT_ONLY
		//
		CREATE "spl" "%BARBARIAN_RAGE%d"
		COPY_EXISTING "%BARBARIAN_RAGE%d.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@3)
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 (BIT9 BOR BIT10 BOR BIT14) // break sanctuary/invisibility, ignore dead/wild magic
			WRITE_LONG 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF ~ADD_SPELL_HEADER~ INT_VAR "range" = 30 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 80 "target" = 2 "duration" = (6 * 3) END // deaf
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "target" = 2 "parameter2" = 112 "duration" = (6 * 3) END // icon: deaf
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 2 "parameter1" = RESOLVE_STR_REF (@4) END // string: deafened
		BUT_ONLY
	END
	// lua
	WITH_SCOPE BEGIN
		OUTER_SET "feedback_strref_already_cast" = RESOLVE_STR_REF (@5)
		OUTER_SET "feedback_strref_trembling_with_fear" = RESOLVE_STR_REF (@6)
		OUTER_SET "feedback_strref_panic" = RESOLVE_STR_REF (@7)
		OUTER_SET "feedback_strref_immune" = RESOLVE_STR_REF (@8)
		//
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\kit\nwn_barbarian_rage.lua" "destRes" = "m_gtspcl" END
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\damage_type.lua" "destRes" = "m_gtsprt" END
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\check_ids.lua" "destRes" = "m_gtsprt" END
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\get_selected_weapon.lua" "destRes" = "m_gtsprt" END
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Eval Conditional" "sourceFileSpec" = "cdtweaks\luke\lib\misc\eval_conditional.lua" "destRes" = "m_gteval" END
	END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lib\resource\tables.lua" "override\m_gttbls.lua"
	END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "gtabmod.2da") BEGIN
		COPY "cdtweaks\luke\lib\2da\gtabmod.2da" "override"
	END
END