DEFINE_ACTION_FUNCTION "PNP_POTIONS" // many thanks to kjeron for help
BEGIN
	////\\\\////\\\\////\\\\////\\\\
	// Add a new spell state
	////\\\\////\\\\////\\\\////\\\\

	LAF "ADD_IDS_ENTRY"
	STR_VAR
		"idsFile" = "SPLSTATE"
		"identifier" = "GT_POTION_IN_USE"
	RET
		"SPLSTATE_POTION_IN_USE" = "value"
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\
	// Add a new custom portrait icon
	////\\\\////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptnicn.bam") BEGIN
			COPY "cdtweaks\bam\gtptnicn.bam" "override"
		END
	END

	LAUNCH_ACTION_FUNCTION "ADD_STATDESC_ENTRY"
	INT_VAR
		"description" = RESOLVE_STR_REF (@0)
	STR_VAR
		"bam_file" = "gtptnicn"
	RET
		"STATDESC_POTION_IN_USE" = "index"
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\
	// Add a new custom school type
	////\\\\////\\\\////\\\\////\\\\////\\\\

	ADD_SCHOOL "GT_POTION" #4294967296 // add silent spell school
	OUTER_SET "MSCHOOL_POTION" = "%GT_POTION%"

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// (Fireball res): Spell dealing 5d10 Fire damage on Self, + 1d10 fire damage in 5' radius (small fireball) (total of 6d10 on Self)
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	// PRO file \\

	WITH_SCOPE BEGIN
		ACTION_MATCH 1 WITH
			(GAME_IS "iwdee") BEGIN
				OUTER_TEXT_SPRINT "pro_res" "IDPRO335"
			END
			(GAME_IS "bgee bg2ee eet") BEGIN
				OUTER_TEXT_SPRINT "pro_res" "FIREBALL"
			END
			DEFAULT
				FAIL "Should not happen"
		END
		//
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtfire05.pro") BEGIN
			COPY_EXISTING "%pro_res%.pro" "override\gtfire05.pro"
				WRITE_SHORT 0x204 (16 * 5) // trigger radius
				WRITE_SHORT 0x206 (16 * 5) // explosion size
			BUT_ONLY
			ADD_PROJECTILE "override\gtfire05.pro"
			//
			COPY_EXISTING "missile.ids" "override"
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "gtfire05" "GT_Fireball_5_ft_Radius"
			BUT_ONLY
		END
	END

	// SPL file \\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptn#00.spl") BEGIN
			CREATE "spl" "gtptn#00"
			COPY_EXISTING "gtptn#00.spl" "override"
				WRITE_LONG NAME1 "-1"
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG DESC "-1"
				WRITE_ASCII 0x3A "spwi304c" #8
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
				WRITE_LONG 0x64 0x72 // Extended Header offset
				WRITE_SHORT 0x68 1 // Extended Header count
				WRITE_LONG 0x6A 0x9A // Feature Block Table offset
				INSERT_BYTES 0x72 0x28
				//
				LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "GT_Fireball_5_ft_Radius") STR_VAR "icon" = "spwi304b" END
				//
				PATCH_IF (GAME_IS "iwdee") BEGIN
					LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 318 "target" = 2 "parameter2" = 63 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource => Evasion check
				END
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 12 "target" = 2 "parameter2" = IDS_OF_SYMBOL ("dmgtype" "FIRE") "timing" = 1 "dicenumber" = 1 "dicesize" = 10 "savingthrow" = BIT24 END // 1d10 fire damage (bypass mirror image)
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 318 "target" = 2 "parameter2" = 44 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource => !SOURCE
				LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 12 "dicenumber" = 5 "dicesize" = 10 STR_VAR "insert" = "last" END // 5d10 fire damage
			BUT_ONLY
		END
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// (Poison res): Spell killing the drinker if not fully immune to poison \\
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptn#01.spl") BEGIN
			CREATE "spl" "gtptn#01"
			COPY_EXISTING "gtptn#01.spl" "override"
				WRITE_LONG NAME1 "-1"
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG DESC "-1"
				WRITE_ASCII 0x3A "sppr411c" #8
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
				WRITE_LONG 0x64 0x72 // Extended Header offset
				WRITE_SHORT 0x68 1 // Extended Header count
				WRITE_LONG 0x6A 0x9A // Feature Block Table offset
				INSERT_BYTES 0x72 0x28
				//
				LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "None") STR_VAR "icon" = "sppr411b" END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 318 "target" = 1 "parameter2" = 77 STR_VAR "resource" = "%DEST_RES%" END // Protection from resource => STAT(RESISTPOISON)>=100
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 215 "target" = 1 "duration" = 1 STR_VAR "resource" = "poison" END // Play visual effect => Over target (unattached)
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 13 "target" = 1 "parameter2" = BIT2 "timing" = 1 END // Kill target => Normal death
			BUT_ONLY
		END
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// (Stat Loss res): Spell causing permanent 1 STR and 1 DEX loss (+ nausea for 1 round) \\
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptn#02.spl") BEGIN
			CREATE "spl" "gtptn#02"
			COPY_EXISTING "gtptn#02.spl" "override"
				WRITE_LONG NAME1 "-1"
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG DESC "-1"
				WRITE_ASCII 0x3A "spwi221c" #8
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
				WRITE_LONG 0x64 0x72 // Extended Header offset
				WRITE_SHORT 0x68 1 // Extended Header count
				WRITE_LONG 0x6A 0x9A // Feature Block Table offset
				INSERT_BYTES 0x72 0x28
				//
				LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "None") STR_VAR "icon" = "spwi221b" END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "timing" = 1 "parameter1" = RESOLVE_STR_REF (@1) END // feedback string
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 15 "target" = 1 "timing" = 1 "parameter1" = "-1" END // Strength bonus
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 44 "target" = 1 "timing" = 1 "parameter1" = "-1" END // Dexterity bonus
				PATCH_IF (MOD_IS_INSTALLED "EEex.tp2" 0) BEGIN
					LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 39 "target" = 1 "parameter2" = 1 "duration" = 6 "special" = 126 "savingthrow" = BIT23 END // Sleep (do not wake upon damage, bypass op101)
				END ELSE BEGIN
					LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 177 "target" = 1 "parameter2" = 2 "duration" = 6 STR_VAR "resource" = "%DEST_RES%" END // Use EFF file (EA.ANYONE)
				END
			BUT_ONLY
			//
			ACTION_IF !(MOD_IS_INSTALLED "EEex.tp2" 0) BEGIN
				CREATE "eff" "gtptn#02"
				COPY_EXISTING "gtptn#02.eff" "override"
					WRITE_LONG 0x10 182 // since this opcode is bugged, we abuse it in order to bypass op101
					WRITE_SHORT 0x2C 100 // probability upper
					WRITE_ASCII 0x70 "gtptnslp" #8 // actual "*.eff" applying sleep
				BUT_ONLY
				CREATE "eff" "gtptnslp"
				COPY_EXISTING "gtptnslp.eff" "override"
					WRITE_LONG 0x10 39 // Sleep
					WRITE_SHORT 0x2C 100 // probability upper
					WRITE_LONG 0x20 1 // do not wake upon damage
					WRITE_LONG 0x48 126 // nauseated icon
				BUT_ONLY
			END
		END
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// (Remove res): Spell containing op220 + op321 for every (permanent / MSCHOOL != GT_POTION) potion effect \\
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptn#03.spl") BEGIN
			CREATE "spl" "gtptn#03"
			COPY_EXISTING "gtptn#03.spl" "override"
				WRITE_LONG NAME1 "-1"
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG DESC "-1"
				WRITE_ASCII 0x3A "spwi513c" #8
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
				WRITE_LONG 0x64 0x72 // Extended Header offset
				WRITE_SHORT 0x68 1 // Extended Header count
				WRITE_LONG 0x6A 0x9A // Feature Block Table offset
				INSERT_BYTES 0x72 0x28
				//
				LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "None") STR_VAR "icon" = "spwi513b" END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "timing" = 1 "parameter1" = RESOLVE_STR_REF (@2) END // feedback string
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 220 "target" = 1 "parameter1" = 9 "parameter2" = "%MSCHOOL_POTION%" "timing" = 1 END // Remove spell school protections
			BUT_ONLY
		END
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// The current potion does nothing... \\
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_IF !(FILE_EXISTS_IN_GAME "gtptn#04.spl") BEGIN
			CREATE "spl" "gtptn#04"
			COPY_EXISTING "gtptn#04.spl" "override"
				WRITE_LONG NAME1 "-1"
				WRITE_LONG NAME2 "-1"
				WRITE_LONG UNIDENTIFIED_DESC "-1"
				WRITE_LONG DESC "-1"
				WRITE_SHORT 0x1C 4 // type: innate
				WRITE_LONG 0x34 1 // level
				WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
				WRITE_LONG 0x64 0x72 // Extended Header offset
				WRITE_SHORT 0x68 1 // Extended Header count
				WRITE_LONG 0x6A 0x9A // Feature Block Table offset
				INSERT_BYTES 0x72 0x28
				//
				LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "None") END
				//
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "timing" = 1 "parameter1" = RESOLVE_STR_REF (@3) END // feedback string
			BUT_ONLY
		END
	END

	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\
	// Get potions to patch \\
	////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\////\\\\

	LAF "GET_POTIONS" RET_ARRAY "potion_list" END

	////\\\\////\\\\////\\\\////\\\\
	// Update potions \\
	////\\\\////\\\\////\\\\////\\\\

	WITH_SCOPE BEGIN
		ACTION_PHP_EACH "potion_list" AS "key" => "value" BEGIN
			COPY_EXISTING "%key_0%.itm" "override"
				LPF "PNP_POTIONS#CORE" END
			BUT_ONLY
		END
	END
END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// Helper functions \\

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

DEFINE_ACTION_FUNCTION "GET_POTIONS"
RET_ARRAY
	"potion_list"
BEGIN
	ACTION_CLEAR_ARRAY "potion_list"
	//
	COPY_EXISTING_REGEXP - "^.+\.itm$" "override"
	//COPY_EXISTING_REGEXP - "^potn[0-9][0-9]\.itm$" "override"
		// SOURCE_SIZE check
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			// DEST_RES check
			PATCH_MATCH "%DEST_RES%" WITH
				"potn47" // Marek's Potion of Antidote
				"potnmare" // Marek's Potion of Antidote
				"ohbptn03" // The Concocter's Poison
				"ohbptn04" // Ceremonial Ale
				"ohbptn10" // Rat Poison
				"ohbptn11" // Myconid Soup
				"potn14" // Oil of Speed
				"potn23" // Oil of Speed (fake)
				"wand16" // Potion of Icedust (AoE potion)
				"chance"  // oil of second chances [iwdee]
				"pnull"   // oil of null effect [iwdee]
				"serpsca" // oil of the serpent's scales [iwdee]
				BEGIN
					//PATCH_PRINT "Skipping '%DEST_FILE%' since it is a special case / oil..."
				END
				DEFAULT
					READ_STRREF NAME2 "identifiedNameString"
					// "Critical item" (plot item) check
					PATCH_IF ((LONG_AT (0x18) BAND IDS_OF_SYMBOL ("itemflag" "INDESTRUCTIBLE")) == 0) BEGIN
						// "Droppable" and "Displayable" checks
						PATCH_IF ((LONG_AT (0x18) BAND IDS_OF_SYMBOL ("itemflag" "DROPPABLE")) AND (LONG_AT (0x18) BAND IDS_OF_SYMBOL ("itemflag" "DISPLAYABLE"))) BEGIN
							// Category check
							PATCH_IF (SHORT_AT (0x1C) == IDS_OF_SYMBOL ("itemcat" "POTION")) BEGIN
								// Identified description check
								PATCH_IF (SLONG_AT (DESC) > 0) BEGIN
									//
									GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
									PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
										// Ability type check
										PATCH_IF (BYTE_AT ("%ab_off%" + 0x0) == 3) BEGIN // Magical
											// Ability target check
											PATCH_IF (BYTE_AT ("%ab_off%" + 0xC) == 5) BEGIN // Caster
												// # effects check
												PATCH_IF (SHORT_AT ("%ab_off%" + 0x1E) > 0) BEGIN
													// Ability projectile check
													PATCH_IF (SHORT_AT ("%ab_off%" + 0x2A) == IDS_OF_SYMBOL ("missile" "None")) BEGIN
														READ_ASCII ("%ab_off%" + 0x4) "ability_icon"
														READ_BYTE ("%ab_off%" + 0x17) "ability_mschool"
														READ_BYTE ("%ab_off%" + 0x19) "ability_msectype"
														DEFINE_ASSOCIATIVE_ARRAY "potion_list" BEGIN
															"%DEST_RES%" , "%ab_ind%" , "%ability_icon%" , "%ability_mschool%" , "%ability_msectype%" => "%identifiedNameString%"
														END
													END
												END
											END
										END
									END
								END
							END
						END
					END
			END
		END
	BUT_ONLY_IF_IT_CHANGES
END

DEFINE_PATCH_FUNCTION "PNP_POTIONS#CORE"
BEGIN
	// update removal spl file
	PATCH_WITH_SCOPE BEGIN
		PATCH_IF ("%key_3%" == 0) BEGIN
			LPF "ALTER_ITEM_HEADER" INT_VAR "header_type" = 3 "header" = ("%key_1%" + 1) "primary_type" = "%MSCHOOL_POTION%" END
		END ELSE BEGIN
			INNER_ACTION BEGIN
				COPY_EXISTING "gtptn#03.spl" "override"
					LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%key_0%" END
				BUT_ONLY
			END
		END
	END
	//
	GET_OFFSET_ARRAY "ab_array" ITM_V10_HEADERS
	//
	PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
		PATCH_IF ("%ab_ind%" == "%key_1%") BEGIN
			GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
			SET "alter_efficacy" = 0
			//
			PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
				READ_SHORT "%fx_off%" "fx_opcode"
				READ_BYTE ("%fx_off%" + 0x2) "fx_target"
				READ_BYTE ("%fx_off%" + 0x3) "fx_power"
				READ_SLONG ("%fx_off%" + 0x4) "fx_parameter1"
				READ_LONG ("%fx_off%" + 0x8) "fx_parameter2"
				READ_BYTE ("%fx_off%" + 0xC) "fx_timing"
				READ_BYTE ("%fx_off%" + 0xD) "fx_resist_dispel"
				READ_LONG ("%fx_off%" + 0xE) "fx_duration"
				READ_BYTE ("%fx_off%" + 0x12) "fx_probability1"
				READ_BYTE ("%fx_off%" + 0x13) "fx_probability2"
				READ_ASCII ("%fx_off%" + 0x14) "fx_resource"
				READ_LONG ("%fx_off%" + 0x1C) "fx_dicenumber"
				READ_LONG ("%fx_off%" + 0x20) "fx_dicesize"
				READ_LONG ("%fx_off%" + 0x24) "fx_savetype"
				READ_SLONG ("%fx_off%" + 0x28) "fx_savebonus"
				READ_LONG ("%fx_off%" + 0x2C) "fx_special"
				PATCH_MATCH "%fx_opcode%" WITH
					61 141 BEGIN END // do not keep track of these visual effects
					174 215 WHEN ("%fx_duration%" < 5) BEGIN END // do not keep track of these (short-duration) cosmetic effects
					139 BEGIN
						LPF "GT_GET_STRING" INT_VAR "strref" = "%fx_parameter1%" RET "string" END
						PATCH_MATCH "%string%" WITH
							"\*gulp\*" "gulp!" BEGIN END // do not keep track of these feedback strings
							DEFAULT
								TEXT_SPRINT $"default_efficacy"("%fx_opcode%" "%fx_target%" "%fx_power%" "%fx_parameter1%" "%fx_parameter2%" "%fx_timing%" "%fx_resist_dispel%" "%fx_duration%" "%fx_probability1%" "%fx_probability2%" "%fx_resource%" "%fx_dicenumber%" "%fx_dicesize%" "%fx_savetype%" "%fx_savebonus%" "%fx_special%" "%fx_ind%" "%ab_ind%") ""
								WRITE_SHORT "%fx_off%" 999 // mark it for later deletion
						END
					END
					12 17 127 159 200 201 218 223 226 227 228 259 331 BEGIN
						TEXT_SPRINT $"default_efficacy"("%fx_opcode%" "%fx_target%" "%fx_power%" "%fx_parameter1%" "%fx_parameter2%" "%fx_timing%" "%fx_resist_dispel%" "%fx_duration%" "%fx_probability1%" "%fx_probability2%" "%fx_resource%" "%fx_dicenumber%" "%fx_dicesize%" "%fx_savetype%" "%fx_savebonus%" "%fx_special%" "%fx_ind%" "%ab_ind%") ""
						WRITE_SHORT "%fx_off%" 999 // mark it for later deletion
						SET "alter_efficacy" = 1
					END
					ANY ("%fx_duration%" > 5) BEGIN
						TEXT_SPRINT $"default_efficacy"("%fx_opcode%" "%fx_target%" "%fx_power%" "%fx_parameter1%" "%fx_parameter2%" "%fx_timing%" "%fx_resist_dispel%" "%fx_duration%" "%fx_probability1%" "%fx_probability2%" "%fx_resource%" "%fx_dicenumber%" "%fx_dicesize%" "%fx_savetype%" "%fx_savebonus%" "%fx_special%" "%fx_ind%" "%ab_ind%") ""
						WRITE_SHORT "%fx_off%" 999 // mark it for later deletion
						SET "alter_efficacy" = 1
					END
					DEFAULT
						TEXT_SPRINT $"default_efficacy"("%fx_opcode%" "%fx_target%" "%fx_power%" "%fx_parameter1%" "%fx_parameter2%" "%fx_timing%" "%fx_resist_dispel%" "%fx_duration%" "%fx_probability1%" "%fx_probability2%" "%fx_resource%" "%fx_dicenumber%" "%fx_dicesize%" "%fx_savetype%" "%fx_savebonus%" "%fx_special%" "%fx_ind%" "%ab_ind%") ""
						WRITE_SHORT "%fx_off%" 999 // mark it for later deletion
				END
			END
			LPF "DELETE_EFFECT" INT_VAR "check_globals" = 0 "header" = "%ab_ind%" "match_opcode" = 999 END
			//
			LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 0 STR_VAR "resource" = "gtptn#00" END // 1% chance to explode
			//
			LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 2 "probability2" = 1 STR_VAR "resource" = "gtptn#01" END // 2% chance for instant poison death
			//
			LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 7 "probability2" = 3 STR_VAR "resource" = "gtptn#02" END // 5% chance for permanent Strength & Dexterity loss (on top of 1 round nausea)
			//
			LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 14 "probability2" = 8 STR_VAR "resource" = "gtptn#03" END // 7% chance to cancel/remove all active potion effects
			//
			LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 24 "probability2" = 15 STR_VAR "resource" = "gtptn#04" END // 10% chance the current potion does nothing -- feedback string
			//
			PATCH_IF "%alter_efficacy%" BEGIN
				LPF "GET_UNIQUE_FILE_NAME" STR_VAR "extension" = "spl" "base" = "cdtweaks-pnp_potions-%DEST_FILE%-minus_50_percent_efficacy" RET "filename" END
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 34 "probability2" = 25 STR_VAR "resource" = "%filename%" END // 10% chance the current potion has -50% efficacy
				LPF "PNP_POTIONS#CORE#ALTER_EFFICACY" STR_VAR "modifier_type" = "half" END
				//
				LPF "GET_UNIQUE_FILE_NAME" STR_VAR "extension" = "spl" "base" = "cdtweaks-pnp_potions-%DEST_FILE%-plus_50_percent_efficacy" RET "filename" END
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 42 "probability2" = 35 STR_VAR "resource" = "%filename%" END // 8% chance the current potion has +50% efficacy
				LPF "PNP_POTIONS#CORE#ALTER_EFFICACY" STR_VAR "modifier_type" = "plus_50_percent" END
				//
				LPF "GET_UNIQUE_FILE_NAME" STR_VAR "extension" = "spl" "base" = "cdtweaks-pnp_potions-%DEST_FILE%-plus_100_percent_efficacy_or_permanent_duration" RET "filename" END
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "timing" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 326 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 43 "probability2" = 43 STR_VAR "resource" = "%filename%" END // 1% chance the current potion has +100% efficacy / permanent duration
				LPF "PNP_POTIONS#CORE#ALTER_EFFICACY" STR_VAR "modifier_type" = "permanent_double" END
				//
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 318 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 43 STR_VAR "resource" = "%DEST_RES%" END // block subsequent effects if one of the above triggers...
				//
				PHP_EACH "default_efficacy" AS "fx_attribute" => "" BEGIN
					LPF "ADD_ITEM_EFFECT" INT_VAR "header" = ("%ab_ind%" + 1) "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = "%fx_attribute_7%" "probability1" = "%fx_attribute_8%" "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
				END
				//
				PATCH_WITH_SCOPE BEGIN
					SET "duration" = 60 // default: 1 turn
					GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" ITM_V10_HEAD_EFFECTS
					PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
						PATCH_MATCH SHORT_AT "%fx_off%" WITH
							20 119 159 200 201 218 223 226 227 228 259 BEGIN END // ignore all effects that can end prematurely
							DEFAULT
								PATCH_IF (LONG_AT ("%fx_off%" + 0xE) > 5) BEGIN
									SET "duration" = LONG_AT ("%fx_off%" + 0xE)
								END
						END
					END
					LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 328 "target" = 1 "parameter2" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "resist_dispel" = (BIT0 | BIT1) "duration" "special" = 1 END // set spell state
					LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 142 "target" = 1 "parameter2" = "%STATDESC_POTION_IN_USE%" "resist_dispel" = (BIT0 | BIT1) "duration" END // portrait icon
				END
			END ELSE BEGIN
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 318 "parameter1" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "parameter2" = 110 "probability1" = 24 STR_VAR "resource" = "%DEST_RES%" END // block subsequent effects if one of the above triggers...
				//
				PHP_EACH "default_efficacy" AS "fx_attribute" => "" BEGIN
					LPF "ADD_ITEM_EFFECT" INT_VAR "header" = ("%ab_ind%" + 1) "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = "%fx_attribute_7%" "probability1" = "%fx_attribute_8%" "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
				END
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 328 "target" = 1 "parameter2" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "resist_dispel" = (BIT0 | BIT1) "duration" = 60 "special" = 1 END // set spell state
				LPF "ADD_ITEM_EFFECT" INT_VAR "target" = 1 "header" = ("%ab_ind%" + 1) "opcode" = 142 "target" = 1 "parameter2" = "%STATDESC_POTION_IN_USE%" "resist_dispel" = (BIT0 | BIT1) "duration" = 60 END // portrait icon
			END
		END
	END
END

DEFINE_PATCH_FUNCTION "PNP_POTIONS#CORE#ALTER_EFFICACY"
STR_VAR
	"modifier_type" = ""
BEGIN
	// make the subspell
	INNER_ACTION BEGIN
		CREATE "spl" "%filename%"
		COPY_EXISTING "%filename%.spl" "override"
			WRITE_LONG NAME1 "-1"
			WRITE_LONG NAME2 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG DESC "-1"
			WRITE_ASCII 0x3A "%key_2%" #8
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			WRITE_LONG 0x18 BIT14 // Ignore dead/wild magic
			WRITE_BYTE 0x25 ("%key_3%" == 0 ? "%MSCHOOL_POTION%" : "%key_3%") // primary type
			WRITE_BYTE 0x27 "%key_4%" // secondary type
			WRITE_LONG 0x64 0x72 // Extended Header offset
			WRITE_SHORT 0x68 1 // Extended Header count
			WRITE_LONG 0x6A 0x9A // Feature Block Table offset
			INSERT_BYTES 0x72 0x28
			//
			LPF "ALTER_SPELL_HEADER" INT_VAR "new_header_type" = 1 "target" = 5 "min_level" = 1 "range" = 0x7FFF "projectile" = IDS_OF_SYMBOL ("missile" "None") STR_VAR "icon" = "%key_2%" END
			// add effects
			PATCH_WITH_SCOPE BEGIN
				PATCH_MATCH "%modifier_type%" WITH
					"half" BEGIN
						LPF "ADD_SPELL_EFFECT" INT_VAR "resist_dispel" = BIT0 | BIT1 "target" = 1 "timing" = 1 "opcode" = 139 "parameter1" = RESOLVE_STR_REF (@4) END // feedback string
						PATCH_WITH_SCOPE BEGIN
							PHP_EACH "default_efficacy" AS "fx_attribute" => "" BEGIN
								PATCH_MATCH "%fx_attribute_0%" WITH
									12 17 127 159 200 201 218 223 226 227 228 259 331 BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = ("%fx_attribute_3%" > 1 ? "%fx_attribute_3%" / 2 : "%fx_attribute_3%") "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = "%fx_attribute_7%" "dicenumber" = ("%fx_attribute_11%" > 1 ? "%fx_attribute_11%" / 2 : "%fx_attribute_11%") "dicesize" = ("%fx_attribute_12%" > 1 ? "%fx_attribute_12%" / 2 : "%fx_attribute_12%") "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
									END
									318 321 324 WHEN ("%fx_attribute_10%" STR_EQ "%key_0%") BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_7%" / 2) "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
									END
									DEFAULT
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_7%" / 2) "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
								END
							END
						END
						// add flag & icon
						PATCH_WITH_SCOPE BEGIN
							SET "duration" = 60 // default: 1 turn
							GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
							PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
								GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
								PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
									PATCH_MATCH SHORT_AT "%fx_off%" WITH
										20 119 159 200 201 218 223 226 227 228 259 BEGIN END // ignore all effects that can end prematurely
										DEFAULT
											PATCH_IF (LONG_AT ("%fx_off%" + 0xE) > 2) BEGIN
												SET "duration" = LONG_AT ("%fx_off%" + 0xE)
											END
									END
								END
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 328 "target" = 1 "parameter2" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "resist_dispel" = (BIT0 | BIT1) "duration" "special" = 1 END // Set spell state
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "target" = 1 "parameter2" = "%STATDESC_POTION_IN_USE%" "resist_dispel" = (BIT0 | BIT1) "duration" END // portrait icon
							END
						END
						// update removal spl file
						PATCH_WITH_SCOPE BEGIN
							PATCH_IF (BYTE_AT 0x25 != "%MSCHOOL_POTION%") BEGIN
								INNER_ACTION BEGIN
									COPY_EXISTING "gtptn#03.spl" "override"
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%filename%" END
									BUT_ONLY
								END
							END
						END
					END
					"plus_50_percent" BEGIN
						LPF "ADD_SPELL_EFFECT" INT_VAR "resist_dispel" = BIT0 | BIT1 "target" = 1 "timing" = 1 "opcode" = 139 "parameter1" = RESOLVE_STR_REF (@5) END // feedback string
						PATCH_WITH_SCOPE BEGIN
							PHP_EACH "default_efficacy" AS "fx_attribute" => "" BEGIN
								PATCH_MATCH "%fx_attribute_0%" WITH
									12 17 127 159 200 201 218 223 226 227 228 259 331 BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = ("%fx_attribute_3%" * 3 / 2) "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = "%fx_attribute_7%" "dicenumber" = ("%fx_attribute_11%" * 3 / 2) "dicesize" = ("%fx_attribute_12%" * 3 / 2) "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
									END
									318 321 324 WHEN ("%fx_attribute_10%" STR_EQ "%key_0%") BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_7%" / 2) "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
									END
									DEFAULT
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_7%" * 3 / 2) "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
								END
							END
						END
						// add flag & icon
						PATCH_WITH_SCOPE BEGIN
							SET "duration" = 60 // default: 1 turn
							GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
							PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
								GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
								PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
									PATCH_MATCH SHORT_AT "%fx_off%" WITH
										20 119 159 200 201 218 223 226 227 228 259 BEGIN END // ignore all effects that can end prematurely
										DEFAULT
											PATCH_IF (LONG_AT ("%fx_off%" + 0xE) > 5) BEGIN
												SET "duration" = LONG_AT ("%fx_off%" + 0xE)
											END
									END
								END
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 328 "target" = 1 "parameter2" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "resist_dispel" = (BIT0 | BIT1) "duration" "special" = 1 END // Set spell state
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "target" = 1 "parameter2" = "%STATDESC_POTION_IN_USE%" "resist_dispel" = (BIT0 | BIT1) "duration" END // portrait icon
							END
						END
						// update removal spl file
						PATCH_WITH_SCOPE BEGIN
							PATCH_IF (BYTE_AT 0x25 != "%MSCHOOL_POTION%") BEGIN
								INNER_ACTION BEGIN
									COPY_EXISTING "gtptn#03.spl" "override"
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%filename%" END
									BUT_ONLY
								END
							END
						END
					END
					"permanent_double" BEGIN
						LPF "ADD_SPELL_EFFECT" INT_VAR "resist_dispel" = BIT0 | BIT1 "target" = 1 "timing" = 1 "opcode" = 139 "parameter1" = RESOLVE_STR_REF (@6) END // feedback string
						PATCH_WITH_SCOPE BEGIN
							PHP_EACH "default_efficacy" AS "fx_attribute" => "" BEGIN
								PATCH_MATCH "%fx_attribute_0%" WITH
									12 17 127 159 200 201 218 223 226 227 228 259 331 BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = ("%fx_attribute_3%" * 2) "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = "%fx_attribute_7%" "dicenumber" = ("%fx_attribute_11%" * 2) "dicesize" = ("%fx_attribute_12%" * 2) "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
									END
									318 321 324 WHEN ("%fx_attribute_10%" STR_EQ "%key_0%") BEGIN
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = "%fx_attribute_5%" "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_7%" / 2) "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%DEST_RES%" END // Remove effects by resource
									END
									174 BEGIN END // no ending sound if the effect is permanent
									DEFAULT
										LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = "%fx_attribute_0%" "target" = "%fx_attribute_1%" "power" = "%fx_attribute_2%" "parameter1" = "%fx_attribute_3%" "parameter2" = "%fx_attribute_4%" "timing" = ("%fx_attribute_5%" == 0 ? 9 : "%fx_attribute_5%") "resist_dispel" = "%fx_attribute_6%" "duration" = ("%fx_attribute_5%" == 0 ? 0 : "%fx_attribute_7%") "dicenumber" = "%fx_attribute_11%" "dicesize" = "%fx_attribute_12%" "savingthrow" = "%fx_attribute_13%" "savebonus" = "%fx_attribute_14%" "special" = "%fx_attribute_15%" STR_VAR "resource" = "%fx_attribute_10%" END
								END
							END
						END
						// add flag & icon
						PATCH_WITH_SCOPE BEGIN
							SET "duration" = 60 // default: 1 turn
							GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
							PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
								GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
								PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
									PATCH_MATCH SHORT_AT "%fx_off%" WITH
										20 119 159 200 201 218 223 226 227 228 259 BEGIN END // ignore all effects that can end prematurely
										DEFAULT
											PATCH_IF (BYTE_AT ("%fx_off%" + 0xC) == 9) BEGIN
												SET "duration" = 0
											END
									END
								END
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 328 "target" = 1 "parameter2" = IDS_OF_SYMBOL ("splstate" "GT_POTION_IN_USE") "resist_dispel" = (BIT0 | BIT1) "timing" = ("%duration%" == 0 ? 9 : 0) "duration" "special" = 1 END // Set spell state
								LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "target" = 1 "parameter2" = "%STATDESC_POTION_IN_USE%" "resist_dispel" = (BIT0 | BIT1) "timing" = ("%duration%" == 0 ? 9 : 0) "duration" END // portrait icon
							END
						END
						// update removal spl file
						PATCH_WITH_SCOPE BEGIN
							GET_OFFSET_ARRAY "ab_array" SPL_V10_HEADERS
							PHP_EACH "ab_array" AS "ab_ind" => "ab_off" BEGIN
								SET "found" = 0
								GET_OFFSET_ARRAY2 "fx_array" "%ab_off%" SPL_V10_HEAD_EFFECTS
								PHP_EACH "fx_array" AS "fx_ind" => "fx_off" BEGIN
									PATCH_MATCH BYTE_AT ("%fx_off%" + 0xC) WITH
										9 BEGIN // op220 cannot remove effects applied with 'timing=9'
											SET "found" = 1
										END
										DEFAULT
									END
								END
								PATCH_IF ("%found%") BEGIN
									INNER_ACTION BEGIN
										COPY_EXISTING "gtptn#03.spl" "override"
											LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%filename%" END
										BUT_ONLY
									END
								END ELSE BEGIN
									PATCH_IF (BYTE_AT 0x25 != "%MSCHOOL_POTION%") BEGIN
										INNER_ACTION BEGIN
											COPY_EXISTING "gtptn#03.spl" "override"
												LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "target" = 1 "timing" = 1 STR_VAR "resource" = "%filename%" END
											BUT_ONLY
										END
									END
								END
							END
						END
					END
					DEFAULT
						PATCH_FAIL "Should not happen"
				END
			END
		BUT_ONLY
	END
END