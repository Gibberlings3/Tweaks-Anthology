DEFINE_ACTION_FUNCTION "PLANAR_TURNING"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"type" = 4
		"level" = 1
	STR_VAR
		"idsName" = "PRIEST_PLANAR_TURNING"
	RET
		"PRIEST_PLANAR_TURNING" = "resName"
	END
	//
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "PRIEST_PLANAR_TURNING"
		//
		CREATE "spl" "%PRIEST_PLANAR_TURNING%"
		COPY_EXISTING "%PRIEST_PLANAR_TURNING%.spl" "override"
			WRITE_LONG NAME1 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 30 "projectile" = IDS_OF_SYMBOL ("projectl" "INAREANP") + 1 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 139 "target" = 1 "parameter1" = RESOLVE_STR_REF (@0) END // feedback message
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 STR_VAR "resource" = "%PRIEST_PLANAR_TURNING%" END // Invoke lua
		BUT_ONLY_IF_IT_CHANGES
	END
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	WITH_SCOPE BEGIN
		OUTER_SET "feedback_strref" = RESOLVE_STR_REF (@1)
		LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\class\planar_turning.lua" "destRes" = "m_gtspcl" END
	END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\check_ids.lua" "destRes" = "m_gtsprt" END
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lib\resource\tables.lua" "override\m_gttbls.lua"
	END
END