DEFINE_ACTION_FUNCTION "METAMAGIC"
BEGIN
	LAF "ADD_EXTENDED_STAT" INT_VAR "max" = 6 STR_VAR "identifier" = "CDTWEAKS_METAMAGIC" END
	//
	WITH_SCOPE BEGIN
		CREATE "eff" "gtmmagic"
		COPY_EXISTING "gtmmagic.eff" "override"
			WRITE_LONG 0x10 214 // 2da spell list
			WRITE_LONG 0x20 3 // from lua
			WRITE_ASCII 0x30 "GTSPMENU" #8 // lua function
		BUT_ONLY
	END
	// auxiliary spell for Silent Spell
	WITH_SCOPE BEGIN
		CREATE "spl" "cd#mmsil"
		COPY_EXISTING "cd#mmsil.spl" "override"
			/* Header */
			WRITE_LONG NAME1 "-1"
			WRITE_LONG UNIDENTIFIED_DESC "-1"
			WRITE_SHORT 0x1C 4 // innate
			WRITE_LONG 0x34 1 // level
			/* Extended Header */
			LPF "ADD_SPELL_HEADER" INT_VAR "target" = 5 "range" = 1000 END
			/* Feature Block(s) */
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 1 STR_VAR "resource" = "GTSILSPL" END // invoke lua
		BUT_ONLY
	END
	//
	WITH_SCOPE BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY "cdtweaks_metamagic" BEGIN
			// bam file / spl resref, bam file (icon), tra ref (icon), tra ref (name), tra_ref (desc) => stat value
			"cdmtmqck", "cdmtmgc1", 0, 1, 2 => 1
			"cdmtmemp", "cdmtmgc2", 3, 4, 5 => 2
			"cdmtmext", "cdmtmgc3", 6, 7, 8 => 3
			"cdmtmmax", "cdmtmgc4", 9, 10, 11 => 4
			"cdmtmsil", "cdmtmgc5", 12, 13, 14 => 5
			"cdmtmstl", "cdmtmgc6", 15, 16, 17 => 6
		END
		//
		ACTION_PHP_EACH "cdtweaks_metamagic" AS "key" => "value" BEGIN
			COPY "cdtweaks\luke\bam\metamagic\%key%.bam" "override" "cdtweaks\luke\bam\metamagic\%key_1%.bam" "override"
			LAF "ADD_STATDESC_ENTRY" INT_VAR "description" = RESOLVE_STR_REF ((AT "%key_2%")) STR_VAR "bam_file" = "%key_1%" RET "index" END
			//
			CREATE "spl" "%key%"
			COPY_EXISTING "%key%.spl" "override"
				/* Header */
				WRITE_LONG NAME1 RESOLVE_STR_REF ((AT "%key_3%"))
				WRITE_LONG UNIDENTIFIED_DESC RESOLVE_STR_REF ((AT "%key_4%"))
				WRITE_ASCII 0x10 "CAS_M02" #8 // casting sound
				WRITE_SHORT 0x1C 4 // innate
				WRITE_LONG 0x34 1 // level
				WRITE_ASCII 0x3A "%key%" #8 // icon
				WRITE_SHORT 0x22 34 // casting animation: swirl white
				/* Extended Header */
				LPF "ADD_SPELL_HEADER" INT_VAR "target" = 7 "range" = 30 STR_VAR "icon" = "%key%" END
				/* Feature Block(s) */
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "timing" = 1 "target" = 1 STR_VAR "resource" = "%DEST_RES%" END // refresh without stacking
				PATCH_FOR_EACH "res" IN "cdmtmqck" "cdmtmemp" "cdmtmext" "cdmtmmax" "cdmtmsil" "cdmtmstl" BEGIN
					PATCH_IF ("%res%" STRING_COMPARE_CASE "%DEST_RES%") BEGIN
						LPF "CLONE_EFFECT" INT_VAR "match_opcode" = 321 "multi_match" = 1 STR_VAR "resource" = "%res%" "insert" = "below" END
					END
				END
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 321 "timing" = 1 "target" = 1 STR_VAR "resource" = "CDMMAGIC" END // remove op408 / op232 (Silent Spell)
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 401 "timing" = 1 "target" = 1 "parameter2" = 1 "parameter1" = "%value%" "special" = IDS_OF_SYMBOL ("stats" "CDTWEAKS_METAMAGIC") END // Set CDTWEAKS_METAMAGIC to "%value%"
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 142 "timing" = 1 "target" = 1 "parameter2" = "%index%" END // portrait icon
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 177 "timing" = 1 "target" = 1 "parameter2" = 2 "parameter1" = IDS_OF_SYMBOL ("EA" "PC") STR_VAR "resource" = "GTMMAGIC" END // use EFF file
				// castable at will \\
				LPF "ADD_SPELL_CFEFFECT" INT_VAR "insert_point" = "-1" "opcode" = 172 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END
				LPF "ADD_SPELL_CFEFFECT" INT_VAR "insert_point" = "-1" "opcode" = 171 "target" = 1 "timing" = 1 STR_VAR "resource" = "%DEST_RES%" END
			BUT_ONLY
		END
	END
	// Listeners
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\metamagic\gain.lua" "destRes" = "m_gtlstn" END // run 'func' each time a sprite has finished evaluating its effects
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\metamagic\utility.lua" "destRes" = "m_gtmmgc" END
	//
	WITH_SCOPE BEGIN
		OUTER_SET "strref_OutOfRange" = RESOLVE_STR_REF (@100)
		OUTER_SET "strref_SpellcastingDisabled" = RESOLVE_STR_REF (@101)
		OUTER_SET "strref_InvisibleOrSanctuaried" = RESOLVE_STR_REF (@102)
		OUTER_SET "strref_CasterSilenced" = RESOLVE_STR_REF (@103)
		OUTER_SET "strref_CasterDoesNotMeetRequirements" = RESOLVE_STR_REF (@104)
		OUTER_SET "strref_AuraFree" = RESOLVE_STR_REF (@105)
		OUTER_SET "strref_SpellDisrupted" = RESOLVE_STR_REF (@106)
		//
		LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Listeners" "sourceFileSpec" = "cdtweaks\luke\lua\metamagic\usage.lua" "destRes" = "m_gtmmgc" END
	END
	//
	LAF "APPEND_LUA_FUNCTION" STR_VAR "description" = "Functions to be invoked via op408" "sourceFileSpec" = "cdtweaks\luke\lua\metamagic\projectile_mutator.lua" "destRes" = "m_gt#408" END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lua\m_gttbls.lua" "override"
	END
END