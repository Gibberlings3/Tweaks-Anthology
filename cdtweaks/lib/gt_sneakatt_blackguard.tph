DEFINE_ACTION_FUNCTION "SNEAKATT_BLACKGUARD"
BEGIN
	LAF "GT_ADD_SPELL"
	INT_VAR
		"type" = 4
		"level" = 1
	STR_VAR
		"idsName" = "BLACKGUARD_SNEAK_ATTACK"
	RET
		"BLACKGUARD_SNEAK_ATTACK" = "resName"
	END
	//
	WITH_SCOPE BEGIN
		ACTION_TO_LOWER "BLACKGUARD_SNEAK_ATTACK"
		//
		CREATE "eff" "%BLACKGUARD_SNEAK_ATTACK%b"
		COPY_EXISTING "%BLACKGUARD_SNEAK_ATTACK%b.eff" "override"
			WRITE_LONG 0x10 402 // invoke lua
			WRITE_SHORT 0x2C 100 // prob1
			WRITE_LONG 0x1C 1 // parameter1
			WRITE_ASCII 0x30 "%BLACKGUARD_SNEAK_ATTACK%" #8 // lua function
		BUT_ONLY
		//
		CREATE "spl" "%BLACKGUARD_SNEAK_ATTACK%b"
		COPY_EXISTING "%BLACKGUARD_SNEAK_ATTACK%b.spl" "override"
			WRITE_LONG NAME1 RESOLVE_STR_REF (@0)
			WRITE_LONG 0x18 BIT14 // flags: ignore dead/wild magic
			WRITE_SHORT 0x1C 4 // type: innate
			WRITE_LONG 0x34 1 // level
			//
			LPF "ADD_SPELL_HEADER" INT_VAR "range" = 30 END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("kit" "barbarian") "parameter2" = 109 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
			PATCH_WITH_SCOPE BEGIN
				LPF "ADD_SPLPROT_ENTRY" INT_VAR "stat" = IDS_OF_SYMBOL ("STATS" "IMMUNITY_TO_BACKSTAB") STR_VAR "value" = "-1" "relation" = "4" "label" = "STAT(IMMUNITY_TO_BACKSTAB) >= n" RET "index" END
				LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = 1 "parameter2" = "%index%" STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (IMMUNITY_TO_BACKSTAB >= 1)
			END
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "plant") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter2" = 55 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (RACE=GOLEM || GENERAL=UNDEAD)
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("general" "weapon") "parameter2" = 103 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message (Animated weapons such as the Mordenkainen's Sword)
			PATCH_WITH_SCOPE BEGIN
				PATCH_FOR_EACH "race" IN "mist" "dragon" "beholder" "slime" "demonic" "mephit" "imp" "elemental" "salamander" "genie" "solar" "antisolar" "planatar" "darkplanatar" BEGIN
					LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 324 "target" = 2 "parameter1" = IDS_OF_SYMBOL ("race" "%race%") "parameter2" = 104 STR_VAR "resource" = "%DEST_RES%" END // Immunity to resource and message
				END
			END
			//
			LPF "ADD_SPELL_EFFECT" INT_VAR "opcode" = 402 "target" = 2 "parameter1" = 2 STR_VAR "resource" = "%BLACKGUARD_SNEAK_ATTACK%" END // invoke lua
		BUT_ONLY_IF_IT_CHANGES
	END
	// Listener: run 'func' each time a sprite has finished evaluating its effects
	OUTER_SET "feedback_strref_weapon_unsuitable" = RESOLVE_STR_REF (@1)
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Class/Kit Abilities" "sourceFileSpec" = "cdtweaks\luke\lua\kit\sneakatt_blackguard.lua" "destRes" = "m_gtspcl" END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\damage_type.lua" "destRes" = "m_gtsprt" END
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Sprites" "sourceFileSpec" = "cdtweaks\luke\lib\sprite\get_selected_weapon.lua" "destRes" = "m_gtsprt" END
	//
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Eval Conditional" "sourceFileSpec" = "cdtweaks\luke\lib\misc\eval_conditional.lua" "destRes" = "m_gteval" END
	LAF "APPEND_LUA_CHUNK" STR_VAR "description" = "Execute Response" "sourceFileSpec" = "cdtweaks\luke\lib\misc\execute_response.lua" "destRes" = "m_gtexec" END
	//
	ACTION_IF !(FILE_EXISTS_IN_GAME "m_gttbls.lua") BEGIN
		COPY "cdtweaks\luke\lib\resource\tables.lua" "override\m_gttbls.lua"
	END
END